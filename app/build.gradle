plugins {
  id "com.android.application"

}
apply plugin: 'kotlin-android'


android {
    compileSdkVersion project.properties.compileSdkVersion.toInteger()
    ndkVersion project.properties.ndkVersion

    buildFeatures {
        viewBinding true
    }
    lintOptions {

        checkReleaseBuilds false

    }
    tasks.withType(JavaCompile) {
        options.fork = true
        options.forkOptions.jvmArgs += ["-Duser.language=en"]
    }
    signingConfigs {
        release {
            keyAlias 'phone1'
            keyPassword '654321'
            storeFile file('D:/Utermux/termux-app-master/签名文件/phone.jks')
            storePassword '123456'
        }
        debug {
            keyAlias 'phone1'
            keyPassword '654321'
            storeFile file('D:/Utermux/termux-app-master/签名文件/phone.jks')
            storePassword '123456'
        }
    }

  /*  sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }*/


    dependencies {
        implementation fileTree(include: ['*.jar'], dir: 'libs')
        implementation "androidx.annotation:annotation:1.2.0"
        implementation "androidx.core:core:1.5.0-rc01"
        implementation "androidx.drawerlayout:drawerlayout:1.1.1"
        implementation "androidx.preference:preference:1.1.1"
        implementation "androidx.viewpager:viewpager:1.0.0"
        implementation "com.google.guava:guava:31.0.1-jre"
        implementation 'com.google.code.gson:gson:2.8.6'
        //网络框架
        implementation 'com.lzy.net:okgo:3.0.4'
        //switch Btn
        implementation 'com.github.iielse:switchbutton:1.0.4'

        implementation 'com.github.getActivity:XXPermissions:11.5'
        implementation 'com.daimajia.numberprogressbar:library:1.4@aar'

        implementation 'me.laoyuyu.aria:core:3.8.16'
        annotationProcessor 'me.laoyuyu.aria:compiler:3.8.16'


        debugImplementation 'com.github.getActivity:Logcat:9.6'

        implementation 'com.scottyab:rootbeer-lib:0.1.0'

        //OTG0
        implementation 'me.jahnen:libaums:0.8.0'
        implementation 'me.jahnen:libaums-httpserver:0.5.3'
        implementation 'me.jahnen:libaums-storageprovider:0.5.1'

        //webview框架
        implementation 'com.just.agentweb:agentweb:4.0.2'
        implementation 'com.just.agentweb:download:4.0.2'


        implementation 'com.mallotec.reb:plugin-locale:1.0.12'
        implementation "io.noties.markwon:core:$markwonVersion"
        implementation "io.noties.markwon:ext-strikethrough:$markwonVersion"
        implementation "io.noties.markwon:linkify:$markwonVersion"
        implementation "io.noties.markwon:recycler:$markwonVersion"

        implementation project(":terminal-view")
        implementation project(":termux-shared")
    }

    defaultConfig {
        applicationId "com.termux"
        minSdkVersion project.properties.minSdkVersion.toInteger()
        targetSdkVersion project.properties.targetSdkVersion.toInteger()
        versionCode 117
        versionName "0.117.17"

        manifestPlaceholders.TERMUX_PACKAGE_NAME = "com.termux"
        manifestPlaceholders.TERMUX_APP_NAME = "Termux"
        manifestPlaceholders.TERMUX_API_APP_NAME = "Termux:API"
        manifestPlaceholders.TERMUX_BOOT_APP_NAME = "Termux:Boot"
        manifestPlaceholders.TERMUX_FLOAT_APP_NAME = "Termux:Float"
        manifestPlaceholders.TERMUX_STYLING_APP_NAME = "Termux:Styling"
        manifestPlaceholders.TERMUX_TASKER_APP_NAME = "Termux:Tasker"
        manifestPlaceholders.TERMUX_WIDGET_APP_NAME = "Termux:Widget"

        externalNativeBuild {
            ndkBuild {
                cFlags "-std=c11", "-Wall", "-Wextra", "-Werror", "-Os", "-fno-stack-protector", "-Wl,--gc-sections"
            }
        }



        ndk {
            abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
        }


        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath true
            }
        }

    }



    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            signingConfig signingConfigs.debug
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    externalNativeBuild {
        ndkBuild {
            path "src/main/cpp/Android.mk"
        }
    }

    lintOptions {
        disable 'ProtectedPermissions'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

}

dependencies {
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation project(path: ':xh_lib')
    testImplementation "junit:junit:4.13.2"
    testImplementation "org.robolectric:robolectric:4.4"
    implementation "androidx.core:core-ktx:+"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

task versionName {
  doLast {
    print android.defaultConfig.versionName
  }
}

def downloadBootstrap(String arch, String expectedChecksum, String version) {
    def digest = java.security.MessageDigest.getInstance("SHA-256")

    def localUrl = "src/main/cpp/bootstrap-" + arch + ".zip"
    def file = new File(projectDir, localUrl)
    if (file.exists()) {
        def buffer = new byte[8192]
        def input = new FileInputStream(file)
        while (true) {
            def readBytes = input.read(buffer)
            if (readBytes < 0) break
            digest.update(buffer, 0, readBytes)
        }
        def checksum = new BigInteger(1, digest.digest()).toString(16)
        if (checksum == expectedChecksum) {
            return
        } else {
            logger.quiet("Deleting old local file with wrong hash: " + localUrl)
            file.delete()
        }
    }

    def remoteUrl = "https://github.com/termux/termux-packages/releases/download/bootstrap-" + version + "/bootstrap-" + arch + ".zip"
    logger.quiet("Downloading " + remoteUrl + " ...")

    file.parentFile.mkdirs()
    def out = new BufferedOutputStream(new FileOutputStream(file))

    def connection = new URL(remoteUrl).openConnection()
    connection.setInstanceFollowRedirects(true)
    def digestStream = new java.security.DigestInputStream(connection.inputStream, digest)
    out << digestStream
    out.close()

    def checksum = new BigInteger(1, digest.digest()).toString(16)
    if (checksum != expectedChecksum) {
        file.delete()
        throw new GradleException("Wrong checksum for " + remoteUrl + ": expected: " + expectedChecksum + ", actual: " + checksum)
    }
}

clean {
    doLast {
        def tree = fileTree(new File(projectDir, 'src/main/cpp'))
        tree.include 'bootstrap-*.zip'
        tree.each { it.delete() }
    }
}

task downloadBootstraps() {
    doLast {
        def version = "2021.12.02-r1"
        downloadBootstrap("aarch64", "69fcd0e30703d9612956cece26f24458aef4f479f00a775237ecaf65eb8ca5e6", version)
        downloadBootstrap("arm",     "db7f46e8af32b60d585865592f59c0043ba3cf445c7a6bf67a08255ab4219526", version)
        downloadBootstrap("i686",    "1c60ce2c68e2a47328686e492b8bc0b3a2a0e0536c6f2894cb4c48ecf1fd3e9e", version)
        downloadBootstrap("x86_64",  "1d80e2613fd35b14d5a0fb8da5b41b38208a1a40fb727bbdb9a8996793bd973e", version)
    }
}

afterEvaluate {
  android.applicationVariants.all { variant ->
    variant.javaCompileProvider.get().dependsOn(downloadBootstraps)
  }
}
repositories {
    mavenCentral()
}
